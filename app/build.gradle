plugins {
    id 'com.android.application'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}
def configuration = rootProject.ext.configuration
android {
    compileSdk configuration.compileSdkVersion
    signingConfigs {
        om {
            storeFile file('../mekeystore.jks')
            storePassword "123456"
            keyAlias "om"
            keyPassword "123456"
        }
    }

    flavorDimensions "build"
    productFlavors {
        om {
            dimension "build"
            applicationIdSuffix ".om"
            manifestPlaceholders = [hostName: "www.om.com"]
            copy {
                from 'src/om/'
                include '*'
                into '.'
            }
            signingConfig signingConfigs.om
        }
    }

    defaultConfig {
        applicationId configuration.package
        minSdkVersion configuration.minSdkVersion
        targetSdkVersion configuration.targetSdkVersion
        versionCode configuration.versionCode
        versionName configuration.versionName
        multiDexEnabled true
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        vectorDrawables.useSupportLibrary = true
        setProperty("archivesBaseName", "app")
    }

    buildFeatures {
        viewBinding true
        buildConfig true
    }
    dataBinding {
        enabled = true
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
        disable 'InvalidPackage'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled false
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            debuggable true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }


    def archiveBuildTypes = ["release", "debug"];
    def distFolder = "${System.properties['user.home']}\\Downloads\\OnlineTradeView\\"
    namespace 'com.onlinetradeview.tv'
    applicationVariants.configureEach { variant ->
        variant.outputs.configureEach { output ->
            if (variant.buildType.name in archiveBuildTypes) {
                if (variant.versionName != null) {
                    String name = "${variant.getFlavorName()}_${variant.buildType.name}_v${variant.versionCode}.apk"
                    outputFileName = new File(name)
                }

                def taskSuffix = variant.name.capitalize()
                def assembleTaskName = "assemble${taskSuffix}"
                if (tasks.findByName(assembleTaskName)) {
                    def copyAPKFolderTask = tasks.create(name: "archive${taskSuffix}", type: Copy) {
                        description "Archive/copy APK folder to a shared folder."
                        def sourceFolder = "$buildDir/outputs/apk/${output.baseName.replace("-", "/")}"
                        def destinationFolder = "$distFolder${output.baseName.replace("-", "/")}"
                        print "Copying APK folder from: $sourceFolder into $destinationFolder\n"
                        from(sourceFolder)
                        into destinationFolder
                        eachFile { file ->
                            file.path = file.name
                        }
                        includeEmptyDirs = false
                    }
                    tasks[assembleTaskName].finalizedBy = [copyAPKFolderTask]
                }
            }
        }
    }
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' } // <--- ADD THIS LINE
    maven {
        url 'https://oss.sonatype.org/content/repositories/ksoap2-android-releases/'
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    api 'com.google.android.material:material:1.13.0'
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.2.0'
    androidTestImplementation 'androidx.test.ext:junit:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.7.0'
    testImplementation 'junit:junit:4.13.2'
    implementation 'androidx.multidex:multidex:2.0.1'
    
    // Image Loading - Optimized
    implementation 'com.github.bumptech.glide:glide:5.0.5'
    annotationProcessor 'com.github.bumptech.glide:compiler:5.0.5'
    
    // Network - Replace Volley with Retrofit
    implementation 'com.squareup.retrofit2:retrofit:3.0.0'
    implementation 'com.squareup.retrofit2:converter-gson:3.0.0'
    implementation 'com.squareup.okhttp3:okhttp:5.3.2'
    implementation 'com.squareup.okhttp3:logging-interceptor:5.3.2'
    
    // Coroutines for async operations
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.10.2'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.10.0'
    
    // Room Database for caching
    implementation 'androidx.room:room-runtime:2.8.4'
    annotationProcessor 'androidx.room:room-compiler:2.8.4'
    implementation 'androidx.room:room-ktx:2.8.4'
    
    // Keep Volley for backward compatibility during transition
    implementation 'com.android.volley:volley:1.2.1'
    
    implementation 'me.grantland:autofittextview:0.2.1'
    implementation 'com.onesignal:OneSignal:5.4.1'
    implementation 'com.google.android.play:app-update:2.1.0'
    implementation 'com.google.android.play:app-update-ktx:2.1.0'
}
